; Changed to remove bug of typecasting from singed char to int - should be from unsigned
; Paul Cawte 03/06/2023

; int getchar(void)
;
; is a standard library function that takes a single input character from standard input.
;
; Parameters:
; None.
; Return Value:
; The input from the standard input is read as an unsigned char and then it is typecast
; and returned as an integer value(int).
; If there is a character in the unget buffer this is returned instead
; Translate CR to LF
; EOF is returned in two cases:
; - When the file end is reached
; - When there is an error during the execution

; BUG warning
; typecasts from signed char, not unsigned char - behaviour left unchanged

	assume	adl=1

	include	"stdio.inc"

	section	.text
	public	_getchar
_getchar:
	ld 	iy, _stdin 		; FILE* pointer for stdin
	ld 	a, (iy+FILE_FH)		; check if stdin still refers
	cp 	a, FH_STDIN 		; to the default (console)
	jr	nz, _call_fgetc

	ld 	a, (iy+FILE_UNGET_CHAR) ; get the unget character if any
	ld 	(iy+FILE_UNGET_CHAR), 0 ; clear any unget character
	or 	a, a
	jr	z, _call_inchar
	sbc	hl, hl
	ld 	l , a
	ret 				; return with unget char (don't translate or echo it)

_call_inchar:
	call	_inchar			; get character from OS - in A if no unget character
	or 	a, a			; clear carry flag
	sbc	hl, hl 			; HL = 0 
	ld	l, a 			; typecast unsigned char (A) to int (HL)

_echo_char:
	push	hl
	call	_outchar		; echo the character to stdout
	pop	hl

_check_cr:
	ld 	a, l
	cp	a, 13			; check if CR
	ret 	nz
	ld 	l, 10 			; translate to LF
	push	hl
	call 	_outchar		; also need to echo LF, otherwise remains on same line 
	pop 	hl
	ret

_call_fgetc:				
	push	iy 			; push the FILE* pointer parameter
	call 	_fgetc 			; int fgetc(FILE *stream), return value in HL
	pop	de 			; clear up the stack
	ret 				; in this case we do not echo the character

	extern	_inchar
	extern	_outchar
	extern 	_stdin
	extern	_fgetc
